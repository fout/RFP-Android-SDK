apply from: '../constants.gradle'

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'io.fabric'

buildscript {
    repositories {
        google()
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'com.google.firebase:firebase-appdistribution-gradle:1.2.0'
        classpath 'io.fabric.tools:gradle:1.31.0'
    }
}

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'jp.fout.rfp.android.demo.kotlin.app.testing.OpenClass'
}

android {
    compileSdkVersion project.ext.demoCompileSdkVersion

    defaultConfig {
        applicationId "jp.fout.rfp.android.demo.kotlin.app"
        minSdkVersion project.ext.demoMinSdkVersion
        targetSdkVersion project.ext.demoTargetSdkVersion
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        versionCode project.ext.demoVersionCode
        versionName project.ext.demoVersionName

        multiDexEnabled true

        missingDimensionStrategy 'exoplayer', 'exoplayer211'

        testInstrumentationRunner "jp.fout.rfp.android.demo.kotlin.app.util.DemoKotlinTestRunner"
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            versionNameSuffix = "-dev"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "javaVersion"
    productFlavors {
        java7 {
            dimension "javaVersion"
            compileOptions {
                targetCompatibility JavaVersion.VERSION_1_7
            }
            missingDimensionStrategy 'exoplayer', 'exoplayer28', 'exoplayer27', 'exoplayer26'
        }
        java8 {
            dimension "javaVersion"
            compileOptions {
                targetCompatibility JavaVersion.VERSION_1_8
            }
            missingDimensionStrategy 'exoplayer', 'exoplayer211', 'exoplayer210', 'exoplayer29'
        }
    }

    lintOptions {
        abortOnError false
        disable 'GoogleAppIndexingWarning'
    }

    testOptions {
        animationsDisabled = true
    }
}

kapt {
    correctErrorTypes = true
}

apply from: 'deps-sdk.gradle'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation "com.android.support:design:28.0.0"
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:customtabs:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.navigation:navigation-fragment:1.0.0'
    implementation 'android.arch.navigation:navigation-fragment-ktx:1.0.0'
    implementation 'android.arch.navigation:navigation-ui:1.0.0'
    //noinspection GradleDependency
    implementation 'com.google.firebase:firebase-core:16.0.8'
    //noinspection GradleDependency
    implementation 'com.google.firebase:firebase-analytics:16.0.8'
    implementation 'com.google.dagger:dagger:2.22.1'
    implementation 'com.google.dagger:dagger-android-support:2.22.1'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.22.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.22.1'
    kapt 'com.google.dagger:dagger-compiler:2.22.1'
    kapt 'com.google.dagger:dagger-android-processor:2.22.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation('com.github.bumptech.glide:glide:4.9.0') {
        exclude group: 'com.android.support'
    }
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
//    androidTestImplementation 'com.android.support.test.espresso.idling:idling-concurrent:3.0.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.3'

    testImplementation 'org.mockito:mockito-core:2.25.0'
    androidTestImplementation 'org.mockito:mockito-android:2.25.0'
}

apply plugin: 'com.google.gms.google-services'
