apply from: '../constants.gradle'

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'com.google.firebase.crashlytics'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.3.4'
        classpath 'com.google.firebase:firebase-appdistribution-gradle:2.0.1'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'
    }
}

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'jp.fout.rfp.android.demo.kotlin.app.testing.OpenClass'
}

android {
    compileSdkVersion project.ext.demoCompileSdkVersion

    defaultConfig {
        applicationId "jp.fout.rfp.android.demo.kotlin.app"
        minSdkVersion project.ext.demoMinSdkVersion
        targetSdkVersion project.ext.demoTargetSdkVersion
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        versionCode project.ext.demoVersionCode
        versionName project.ext.demoVersionName

        multiDexEnabled true

        missingDimensionStrategy 'exoplayer', 'exoplayer211'

        testInstrumentationRunner "jp.fout.rfp.android.demo.kotlin.app.util.DemoKotlinTestRunner"
    }

    buildFeatures {
        dataBinding = true
    }

    buildTypes {
        debug {
            versionNameSuffix = "-dev"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "javaVersion"
    productFlavors {
        java7 {
            dimension "javaVersion"
            compileOptions {
                targetCompatibility JavaVersion.VERSION_1_7
            }
            missingDimensionStrategy 'exoplayer', 'exoplayer28', 'exoplayer27', 'exoplayer26'
        }
        java8 {
            dimension "javaVersion"
            compileOptions {
                targetCompatibility JavaVersion.VERSION_1_8
            }
            missingDimensionStrategy 'exoplayer', 'exoplayer211', 'exoplayer210', 'exoplayer29'
        }
    }

    lintOptions {
        abortOnError false
        disable 'GoogleAppIndexingWarning'
    }

    testOptions {
        animationsDisabled = true
    }
}

kapt {
    correctErrorTypes = true
}

apply from: 'deps-sdk.gradle'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //noinspection GradleCompatible
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.2'
    implementation platform('com.google.firebase:firebase-bom:26.2.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.dagger:dagger:2.27'
    implementation 'com.google.dagger:dagger-android-support:2.27'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.27'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.27'
    kapt 'com.google.dagger:dagger-compiler:2.27'
    kapt 'com.google.dagger:dagger-android-processor:2.27'
    //noinspection GradleDependency
    implementation 'javax.xml.bind:jaxb-api:2.3.0'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.0.1'
    implementation 'com.sun.activation:javax.activation:1.2.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation('com.github.bumptech.glide:glide:4.11.0') {
        exclude group: 'com.android.support'
    }
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    testImplementation "junit:junit:${project.ext.junitVersion}"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
//    androidTestImplementation 'com.android.support.test.espresso.idling:idling-concurrent:3.0.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'

    testImplementation "org.mockito:mockito-core:${project.ext.mockitoVersion}"
    androidTestImplementation "org.mockito:mockito-android:${project.ext.mockitoVersion}"
}

apply plugin: 'com.google.gms.google-services'
